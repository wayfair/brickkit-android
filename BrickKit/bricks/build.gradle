plugins {
    id 'com.android.library'
    id 'checkstyle'
    id 'maven-publish'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures.dataBinding = true

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.0.1'

    implementation 'androidx.core:core:1.3.1'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    testImplementation 'junit:junit:4.13'

    kaptAndroidTest 'androidx.databinding:databinding-compiler:4.1.0'
    androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation('org.mockito:mockito-android:3.5.10')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0') {
        exclude group: 'androidx.annotation', module: 'annotation'
    }
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
}

check.dependsOn("checkstyle")

gradle.projectsEvaluated {
    createDebugAndroidTestCoverageReport.dependsOn("connectedDebugAndroidTest")
}

allOpen {
    annotation 'com.wayfair.brickkit.OpenForTesting'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src/main'
    include '**/*.java'
    classpath = files() as FileCollection
}

static def getCurrentVersion() {
    return "0.9.73"
}

// This is mandatory
group = 'com.wayfair'
version = getCurrentVersion()

publishing {
    publications {
        library(MavenPublication) {
            groupId 'com.wayfair'
            artifactId 'brickkit-android'
            version getCurrentVersion()
            pom {
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
            }

            afterEvaluate {
                artifact bundleReleaseAar
            }
        }
    }
}

bintray {
    publish true
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['library']

    pkg {
        userOrg = 'wayfair'
        repo = 'brickkit-android'
        name = 'brickkit-android'

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/wayfair/brickkit-android.git'
        publicDownloadNumbers = false
        version {
            name = currentVersion
            desc = 'Feature Release of brickkit-android'
            vcsTag = currentVersion
            released = new Date()
        }
    }
}
