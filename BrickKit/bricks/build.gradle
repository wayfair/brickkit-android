plugins {
    id 'com.android.library'
    id 'checkstyle'
    id 'maven'
    id 'maven-publish' // We should switch from the maven plugin to this one
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures.dataBinding = true

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'ObsoleteLintCustomCheck'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72'

    implementation 'androidx.core:core:1.3.1'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    testImplementation 'junit:junit:4.13'

    kaptAndroidTest 'androidx.databinding:databinding-compiler:4.1.0'
    androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation('org.mockito:mockito-android:3.5.10')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0') {
        exclude group: 'androidx.annotation', module: 'annotation'
    }
}

check.dependsOn("checkstyle")

gradle.projectsEvaluated {
    createDebugAndroidTestCoverageReport.dependsOn("connectedDebugAndroidTest")
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src/main'
    include '**/*.java'
    classpath = files() as FileCollection
}

def getCurrentVersion() {
    return "git describe --abbrev=0 --tag".execute().text.trim();
}

task createPom {
    doLast {
        pom {
            project {
                groupId 'com.wayfair'
                artifactId 'brickkit-android'
                version getCurrentVersion()
                packaging 'aar'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("build/brickkit-android-" + getCurrentVersion() + ".pom")
    }
}


task createBintrayDescriptor {
    String currentVersion = getCurrentVersion();

    String buildDir = projectDir.toString() + "/build/"
    if (!new File(buildDir).exists()) {
        new File(buildDir).mkdir()
    }

    File outputFile = new File(buildDir + "bintray.descriptor")
    Date date = new Date()

    outputFile.text =
            '''{
    "package": {
        "name": "brickkit-android",
        "repo": "brickkit-android",
        "subject": "wayfair",
        "desc": "RecyclerView wrapper library",
        "website_url": "http://engineering.wayfair.com/",
        "issue_tracker_url": "https://github.com/wayfair/brickkit-android/issues",
        "vcs_url": "https://github.com/wayfair/brickkit-android.git",
        "github_use_tag_release_notes": true,
        "github_release_notes_file": "LICENSE",
        "licenses": ["Apache License, Version 2.0"],
        "labels": [],
        "public_download_numbers": false,
        "public_stats": false,
        "attributes": []
    },

    "version": {
        "name": "''' + currentVersion + '''",
        "desc": "Initial Release of brickkit-android",
        "released": "''' + date.format('yyyy-MM-dd') + '''",
        "vcs_tag": "''' + currentVersion + '''",
        "attributes": [],
        "gpgSign": false
    },

    "files":
        [
        {"includePattern": "BrickKit/bricks/build/outputs/aar/bricks-release.aar", "uploadPattern":"com/wayfair/brickkit-android/''' + currentVersion + '''/brickkit-android-''' + currentVersion + '''.aar"},
        {"includePattern": "BrickKit/bricks/build/brickkit-android-''' + currentVersion + '''.pom", "uploadPattern":"com/wayfair/brickkit-android/''' + currentVersion + '''/brickkit-android-''' + currentVersion + '''.pom"}
        ],
    "publish": true
}
'''
}

// For local development, use publishToMavenLocal task and mavenLocal() in the consuming app's repositories
project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                groupId 'com.wayfair'
                artifactId 'brickkit-android'
                version = getCurrentVersion() // or hard-code this to be certain which version you're consuming

                artifact bundleReleaseAar
            }
        }
    }
}
